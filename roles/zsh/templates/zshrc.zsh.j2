#!/usr/local/bin/zsh

# Set ZSH to the path to these dotfiles
export ZSH={{ inventory_dir }}

# Set config_files to every file in the zsh folder ending in .zsh
typeset -U config_files
config_files=($ZSH/**/zsh/*.zsh)

# First source any file called 'path.zsh'
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# Now source anything '*.zsh' that is not 'path.zsh' or 'completion.zsh'
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source "$file"
done

# Add our completions to fpath
fpath=(/usr/local/share/zsh/completions/src $fpath)

# This line initializes the completion system
autoload -U +X compinit && compinit

# This line lets zsh use bash completions
autoload -U +X bashcompinit && bashcompinit

# Now source all 'completion.zsh' files. These must be loaded last.
for file in ${(M)config_files:#*/completion.zsh}
do
  source "$file"
done

# We don't need this variable anymore
unset config_files

# Load autosuggestions and syntax-highlighting
# Syntax-highlighting must be loaded at the end
source /usr/local/share/zsh/autosuggestions/zsh-autosuggestions.zsh
source /usr/local/share/zsh/syntax-highlighting/zsh-syntax-highlighting.zsh

# Lastly we load our prompt
eval "$(starship init zsh)"